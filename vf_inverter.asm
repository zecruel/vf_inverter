;*************************************************************
;** SIMPLE SCALAR (V/F) INVERTER CONTROL OF INDUCTION MOTOR **
;** VIA PWM TECHNIQUE                                       **
;**                                                         **
;** AUTHOR: EZEQUIEL RABELO DE AGUIAR  (ZECRUEL@IG.COM.BR)  **
;** DATE: FEB/2014                                          **
;** TARGET: 8051 ARCHITECTURE (FITS ON AT89C2051)	    **
;** CHARACTERISTICS:                                        **
;**    OUT FREQUENCIES AVAILABLE: 60,50,40,30,20 AND 10Hz   **
;**    CRYSTAL = 11.0592MHz                                 **
;**    INPUT = ONE BUTTON (P3.7)                            **
;**    OUTPUTS = SIX LED INDICATORS (P3.0 TO P3.5)          **
;**              PWM OUT (P1.0)                             **
;**              POLARITY OUT (P1.1)                        **
;**                                                         **
;*************************************************************

;======== CONSTANTS=====================
	;VALUES OF THE AMPLITUDE STEP OF THE SAWTOOTH WAVE, ACCORDING TO THE FREQUENCY
	STEP_60 EQU 4D 
	STEP_50 EQU 5D
	STEP_40 EQU 6D
	STEP_30 EQU 8D
	STEP_20 EQU 11D
	STEP_10 EQU 23D
	
	;VALUE TO BE RELOADED IN SAWTHOOTH´S TIMER
	;SAW FREQ = 1200Hz IN 10 STEPS
	T_SAW EQU 179D
	
	;VALUES TO BE RELOADED IN SINE´S TIMER, ACCORDING TO THE FREQUENCY
	T_60 EQU 65152D
	T_50 EQU 65075D
	T_40 EQU 64960D
	T_30 EQU 64768D
	T_20 EQU 64384D
	T_10 EQU 63232D
	
;======== VARIABLES IN INTERNAL RAM=====================
	SINE EQU 20H 		;SINE'S CURRENT VALUE
	COUNT_SIN EQU 21H 	;COUNTER FOR THE SINE'S LOOCKUP TABLE
	SAWTOOTH EQU 22H	;SAWTHOOTH´S CURRENT VALUE
	COUNT_SAW EQU 23H	;COUNTER FOR THE SAWTHOOTH WAVE GENERATOR
	STEP_SAW EQU 24H	;CURRENT STEP FOR THE SAW'S COUNTER, ACCORDING TO THE FREQUENCY
	T_SIN_L EQU 25H		;VALUE TO BE RELOADED IN SINE´S TIMER, LOW BYTE
	T_SIN_H EQU 26H		;VALUE TO BE RELOADED IN SINE´S TIMER, HIGH BYTE
	FREQ EQU 27H		;CURRENT FREQUENCY INDEX, WHERE 0 = 60HZ ... 5 = 10HZ

;======= FLOW AFTER RESET AND INTERRUPTS ===========
;======= 8051 ARCHITETURE =======================
	ORG 0000H
	SJMP START		;RESET-> PROGRAM INITIALIZATION

	;ORG 03H		;IE0 INTERRUPT

	ORG	0BH		;T0 INTERRUPT
	SJMP	T0_INT		; -> SINE WAVE GENERATION

	;ORG 13H		;IE1 INTERRUPT

	ORG	1BH		;T1 INTERRUPT
	SJMP	T1_INT		; -> SAW WAVE GENERATION AND PWM OUTPUT
	
	;ORG	23H		; SERIAL INTERRUPT
	;ORG	2BH		; T2 INTERRUPT
	
;===== SINE WAVE GENERATION =======
;THE SINE WAVE IS USED AS A COMPARATIVE REFERENCE IN THE PWM OUTPUT.
;THE SINE VALUE IS PERIODICALLY UPDATED BY T0 INTERRUPTION CALL,
;VIA LOOKUP TABLE TECHNIQUE. THE TIME OF INTERRUPTION VARIES, 
;DEPENDING ON THE SELECTED OUTPUT FREQ.

T0_INT:
	CLR TR0			;STOP THE TIMER
	MOV DPTR, #SINE_WAVE
	MOV A, COUNT_SIN
	MOVC A, @A+DPTR		;SEE THE VALUE OF COUNT_SIN INDEX IN THE SINE_WAVE TABLE
	MOV SINE, A		;AND STORE IN SINE VARIABLE
	
	MOV C, SINE.7		;HIGH BIT OF VALUE IS THE POLARITY
	MOV P1.1,C		;SEND IT TO THE OWN OUTPUT
	CLR SINE.7		;AND CLEAR IT
	
	INC COUNT_SIN		;GO TO NEXT VALUE
	MOV A, COUNT_SIN
	
	CJNE A, #40, QUIT_T0	;CHECK IF TABLE ARE AT THE END
	MOV COUNT_SIN, #0	;THEN, RESTART THE TABLE COUNT
	
QUIT_T0:
	MOV	TL0, T_SIN_L	;RELOAD THE TIMER COUNTER
	MOV	TH0, T_SIN_H
	SETB TR0		;RESTART THE TIMER
	RETI

;===== SAW WAVE GENERATION  AND PWM OUTPUT =======
;THE FREQUENCY OF SAWTHOOTH WAVE IS FIXED AT 1200Hz,
;AND THIS SIGNAL IS GENERATE WITH 10 STEPS.

T1_INT:
	;FIRST, THE PWM!
	MOV A,SAWTOOTH		;COMPARES THE SAW WAVE
	SUBB A,SINE		;VERSUS THE SINE WAVE
	MOV P1.0,C		;<==== PMW OUTPUT

	;CONTINUE THE SAWTOOTH WAVE GENERATION
	MOV A, SAWTOOTH		
	ADD A, STEP_SAW		;INCREMENT THE SAW WIHT CURRENT STEP
	MOV SAWTOOTH, A
	
	DJNZ COUNT_SAW, QUIT_T1	;CHECK IT'S DONE
	MOV SAWTOOTH, #0	;THEN, RESTART THE WAVE
	MOV COUNT_SAW, #10D	;AND RESET ITS COUNTER
QUIT_T1:
	DEC R0			;R0 FOR BUTTON DEBOUNCE ROUTINE
	RETI

;=========== INITIALIZATION =============
START:
	;INITIALIZE VARIABLES FOR 60Hz OUTPUT
	MOV	COUNT_SIN, #0H
	MOV STEP_SAW,#STEP_60
	MOV T_SIN_L,#LOW(T_60)
	MOV T_SIN_H,#HIGH(T_60)
	MOV FREQ, #0
	CLR P3.0 		;<--- LED INDICATOR
	
	;INITIALIZE SAW'S VARIABLES
	MOV SAWTOOTH,#0
	MOV	COUNT_SAW, #10D
	
	;INITIALIZE TIMERS
	MOV	TL0, T_SIN_L
	MOV	TH0, T_SIN_H
	MOV	TH1, #T_SAW
	MOV	TMOD, #21H	;TIMER0 MODE 1, TIMER1 MODE 2
	SETB ET0		;ENABLE T0 INTERRUPT
	SETB ET1		;ENABLE T1 INTERRUPT
	SETB EA			;ENABLE INTERRUPTS
	SETB	TR0		;START TIMER 0
	SETB 	TR1		;START TIMER 1
	
	MOV P1,#0		;CLEAR OUTPUT

;============ MAIN LOOP (OR IDLE STATE) ===========
MAIN:

	;----------- USER INTERFACE  -------
	;USER ACTION -> PRESS THE BUTTON FOR CHANGE THE OUTPUT FREQ
	;INDICATION -> ONE LED FOR EVERY FREQ AVAILABLE
	
WAIT_PRESS:
	JB P3.7,WAIT_PRESS	;CHECK IF BUTTON WAS PRESSED
	MOV R0, #01H		;THEN, WAIT THE HOLD TIME (DEBOUNCE), VIA TIMER 1
HOLD_TIME:
	JB P3.7, WAIT_PRESS	;BUTTON STILL PRESSED
	CJNE R0,#0, HOLD_TIME	;AND HOLD TIME ELAPSED
	
	;PRESSED BUTTON FOR SURE, NEXT: SELECT THE OUTPUT FREQ

	MOV R1,FREQ		;CHECK THE PAST FREQ
F_60:
	CJNE R1,#0, F_50 	;IF PAST FREQ IS 60HZ (INDEX 0)
	MOV FREQ,#1		;CHANGE TO 50Hz (INDEX 1)
	MOV STEP_SAW,#STEP_50	;CHANGE THE AMPLITUDE OF SAW WAVE
	MOV T_SIN_L,#LOW(T_50)	;CHANGE THE FREQ OF SINE WAVE
	MOV T_SIN_H,#HIGH(T_50)	;
	ORL P3,#00111111B	;CLEAR ALL LEDS
	CLR P3.1		;<--- LED INDICATOR FOR 50HZ
	SJMP RELEASE_B		;WAIT THE BUTON RELEASE
	
	;FOR THE OTHER FREQS, THE PROCEDURE IS SIMILAR
F_50:
	CJNE R1,#1,F_40		
	MOV FREQ,#2
	MOV STEP_SAW,#STEP_40
	MOV T_SIN_L,#LOW(T_40)
	MOV T_SIN_H,#HIGH(T_40)
	ORL P3,#00111111B
	CLR P3.2
	SJMP RELEASE_B
F_40:
	CJNE R1,#2,F_30
	MOV FREQ,#3
	MOV STEP_SAW,#STEP_30
	MOV T_SIN_L,#LOW(T_30)
	MOV T_SIN_H,#HIGH(T_30)
	ORL P3,#00111111B
	CLR P3.3
	SJMP RELEASE_B
F_30:
	CJNE R1,#3,F_20
	MOV FREQ,#4
	MOV STEP_SAW,#STEP_20
	MOV T_SIN_L,#LOW(T_20)
	MOV T_SIN_H,#HIGH(T_20)
	ORL P3,#00111111B
	CLR P3.4
	SJMP RELEASE_B
F_20:
	CJNE R1,#4,F_10
	MOV FREQ,#5
	MOV STEP_SAW,#STEP_10
	MOV T_SIN_L,#LOW(T_10)
	MOV T_SIN_H,#HIGH(T_10)
	ORL P3,#00111111B
	CLR P3.5
	SJMP RELEASE_B
F_10:
	;IF PAST FREQ IS 10HZ (FOR EXCLUSION)
	;THEN BACK TO 60HZ
	MOV FREQ,#0
	MOV STEP_SAW,#STEP_60
	MOV T_SIN_L,#LOW(T_60)
	MOV T_SIN_H,#HIGH(T_60)
	ORL P3,#00111111B
	CLR P3.0
	
	;--------------------------------------------------
RELEASE_B:
	JNB P3.7,RELEASE_B	;WAIT THE BUTON RELEASE

AJMP	MAIN	;INFINITE LOOP

;======== SINE WAVE TABLE ==================
;FULL CYCLE OF SINE WAVE, SAMPLED AT 40 POINTS
;MOST SIGNIFICANT BIT INDICATES NEGATIVE POLARITY
SINE_WAVE: 
	DB 0,6,11,17,22,26,30,33,35,37,37,37,35,33,30,26,22,17,11,6,0,134,139,145,150,154,158,161,163,165,165,165,163,161,158,154,150,145,139,134

;======================================
END